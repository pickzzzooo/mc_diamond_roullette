command /디버프 [<text>] [<text>]:
	trigger:
		if {디버프리스트::*} is not set:
			setDebuffList()

		if arg-1 is not set:
			message "&5[다이아룰렛] &f랜덤 디버프 부여 목록"
			loop indices of {디버프리스트::*}:
				set {_key} to loop-value
				set {_value} to {디버프리스트::%{_key}%}
				if {_value} is true:
					message "&a%{_key}% - %{_value}%"
				else if {_value} is false:
					message "&c%{_key}% - %{_value}%"
		else if arg-1 is "수정":
			if arg-2 is not set:
				message "&c[다이아룰렛] &c/디버프 수정 (디버프)"
			else:
				loop indices of {디버프리스트::*}:
					if arg-2 is loop-value:
						set {_pulse} to true
						set {_key} to loop-value
						set {_value} to {디버프리스트::%{_key}%}
						if {_value} is true:
							set {디버프리스트::%{_key}%} to false
							message "&5[다이아룰렛] &a%{_key}% -> &c%{_key}%"
						else if {_value} is false:
							set {디버프리스트::%{_key}%} to true
							message "&5[다이아룰렛] &c%{_key}% -> &a%{_key}%"
				if {_pulse} is not set:
					message "&c[다이아룰렛] 유효하지 않은 디버프. /디버프 입력해서 리스트를 확인해주세요."

		else if arg-1 is "부여":
			if arg-2 is not set:
				message "&c[다이아룰렛] &c/디버프 부여 (디버프)"
			else:
				loop indices of {디버프리스트::*}:
					if arg-2 is loop-value:
						set {_pulse} to true
						pickDebuff(loop-value)
				if {_pulse} is not set:
					message "&c[다이아룰렛] 유효하지 않은 디버프. /디버프 입력해서 리스트를 확인해주세요."
		else if arg-1 is "랜덤":
			set {_가능한디버프::*} to indices of {디버프리스트::*}
			loop {_가능한디버프::*}:
				if {디버프리스트::%loop-value%} is not true:
					remove loop-value from {_가능한디버프::*}

			if size of {_가능한디버프::*} > 0:
				set {_선택된디버프} to random element out of {_가능한디버프::*}
				pickDebuff({_선택된디버프})
		else:
			message "&c[다이아룰렛] &c/디버프 (랜덤/부여/수정) (값)"

function pickDebuff(name: text):
	if {_name} is "감속":
		apply slowness 1 to {플레이어} for 30 seconds
	else if {_name} is "나약함":
		apply weakness 1 to {플레이어} for 30 seconds
	else if {_name} is "독":
		apply poison 1 to {플레이어} for 30 seconds
	else if {_name} is "위더":
		apply wither 1 to {플레이어} for 30 seconds
	else if {_name} is "채굴 피로":
		apply mining fatigue 1 to {플레이어} for 30 seconds
	else if {_name} is "실명":
		apply blindness 1 to {플레이어} for 30 seconds
	else if {_name} is "허기":
		apply hunger 1 to {플레이어} for 30 seconds
	else if {_name} is "공중 부양":
		apply levitation 1 to {플레이어} for 30 seconds
	else if {_name} is "어둠":
		apply darkness 1 to {플레이어} for 30 seconds




function setDebuffList():
	set {디버프리스트::감속} to true
	set {디버프리스트::나약함} to true
	set {디버프리스트::독} to true
	set {디버프리스트::위더} to true
	set {디버프리스트::채굴 피로} to true
	set {디버프리스트::실명} to true
	set {디버프리스트::허기} to true
	set {디버프리스트::공중 부양} to true
	set {디버프리스트::어둠} to true



